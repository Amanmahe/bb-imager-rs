_PACKAGE_NAME_SERVICE := 'bb-imager-service'

_RELEASE_DIR_SERVICE := _RELEASE_DIR / _PACKAGE_NAME_SERVICE / VERSION

_SERVICE_ASSETS := source_directory() / 'assets'


# Release build for Beagle Board Imager GUI. Should be used for final testing
[group('build')]
build-service TARGET=_HOST_TARGET:
	@echo "Build Beagle Board Imager Service for {{TARGET}}"
	{{RUST_BUILDER}} build --release --target {{TARGET}} -p bb-imager-service


# Create debian package for Beagle Board Imager Service
[group('packaging')]
package-service-linux-deb TARGET=_HOST_TARGET: (build-service TARGET) (_package-linux-deb 'bb-imager-service' TARGET _RELEASE_DIR_SERVICE / _PACKAGE_NAME_SERVICE + '-' + TARGET + '.deb')


# Create compressed Beagle Board Imager service package for Linux
[group('packaging')]
package-service-linux-xz TARGET=_HOST_TARGET: (build-service TARGET)
	@echo "Create compressed Beagle Board Service Package for Linux"
	install -D target/{{TARGET}}/release/bb-imager-service -t {{_RELEASE_DIR_CLI}}/bb-imager-service-tempdir/usr/bin/
	install -D -m 644 {{_SERVICE_ASSETS}}/dbus-org.beagleboard.ImagingService.service -t {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir/usr/lib/systemd/system/
	install -D -m 644 {{_SERVICE_ASSETS}}/org.beagleboard.ImagingService.conf -t {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir/usr/share/dbus-1/system.d/
	install -D -m 644 {{_SERVICE_ASSETS}}/org.beagleboard.ImagingService.policy -t {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir/usr/share/polkit-1/actions/
	install -D -m 644 {{_SERVICE_ASSETS}}/org.beagleboard.ImagingService.service -t {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir/usr/share/dbus-1/system-services/
	cd {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir/ && tar cfJ {{_RELEASE_DIR_SERVICE}}/{{_PACKAGE_NAME_SERVICE}}-{{TARGET}}.tar.xz *
	rm -rf {{_RELEASE_DIR_SERVICE}}/bb-imager-service-tempdir
